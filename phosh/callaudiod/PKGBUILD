_mode=host
pkgname=callaudiod
pkgdesc="Call audio routing daemon"
pkgver=0.1.4.r0.ga7ca6ce
pkgrel=1
_arches=all
arch=(
    x86_64
    aarch64
)
license=(GPL3)
url=https://gitlab.com/mobian1/callaudiod
depends=(
    alsa-lib
    glib2
    libpulse
)
makedepends=(meson)
_commit=a7ca6ce9d4c947f19c3f99ff2cab986c64434e57
source=("git+$url#commit=$_commit")
sha256sums=(SKIP)

pkgver() {
    cd "$srcdir/$pkgname"
    git describe --long "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd $pkgname
    arch-meson . output
    ninja -C output
}

package() {
    cd $pkgname
    DESTDIR="$pkgdir" ninja -C output install
}
