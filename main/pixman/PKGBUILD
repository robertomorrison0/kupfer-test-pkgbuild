# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>
#
# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable x86 extensions (mmx, sse2, ssse3)
#
# Kupfer: add pixman aarch64 vector patch
_mode=host
pkgname=pixman
pkgdesc="The pixel-manipulation library for X and cairo"
pkgver=0.40.0
pkgrel=2
_arches=specific
arch=(
    aarch64
)
license=(custom)
url=https://cgit.freedesktop.org/pixman/
provides=(libpixman-1.so)
depends=(glibc)
makedepends=(
    meson
    libpng
)
source=(
    https://xorg.freedesktop.org/releases/individual/lib/${pkgname}-${pkgver}.tar.xz
    https://gitlab.freedesktop.org/pixman/pixman/-/commit/04f662a3ee0dbab1b4755a6faf491751f6f29627.patch
)
sha256sums=(
    da8ed9fe2d1c5ef8ce5d1207992db959226bd4e37e3f88acf908fd9a71e2704e
    e131682707ab97817e9dda33d36be0c4b17be0171889d10b0b32c6ad9585014b
)

#validpgpkeys=('') # Maarten Lankhorst <maarten.lankhorst@linux.intel.com>

build() {
  cd "$srcdir/pixman-$pkgver"
  patch -p1 -i "$srcdir/04f662a3ee0dbab1b4755a6faf491751f6f29627.patch"
  arch-meson build \
    -D loongson-mmi=disabled \
    -D vmx=disabled \
    -D arm-simd=disabled \
    -D neon=disabled \
    -D a64-neon=enabled \
    -D iwmmxt=disabled \
    -D mmx=disabled \
    -D sse2=disabled \
    -D ssse3=disabled \
    -D mips-dspr2=disabled \
    -D gtk=disabled
  ninja -C build
}

check() {
  #  meson test -C build
  true
}

package() {
  cd "$srcdir/pixman-$pkgver"
  DESTDIR="$pkgdir" meson install -C build
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 COPYING
}
