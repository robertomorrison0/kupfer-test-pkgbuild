# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Kupfer: InsanePrawn
_mode=host
pkgbase=modemmanager-qcom
pkgname=(
    modemmanager-qcom
    libmm-glib-qcom
)
pkgdesc="Mobile broadband modem management service"
pkgver=1.18.12
pkgrel=1
_arches=specific
arch=(
    aarch64
    armv7h
)
license=(
    GPL2
    LGPL2.1
)
url="https://www.freedesktop.org/wiki/Software/ModemManager/"
depends=(
    systemd
    libgudev
    polkit
    ppp
    libqmi
    libmbim
    mobile-broadband-provider-info
)
makedepends=(
    gtk-doc
    gobject-introspection
    vala
    meson
    git
    bash-completion
)
checkdepends=(
    python-gobject
    python-dbus
)
options=(debug)
_commit=c234bd55c9d9618c1478b5e80aaf4b8f965be181  # tags/1.18.12^0
source=("git+https://gitlab.freedesktop.org/mobile-broadband/ModemManager.git#commit=$_commit")
sha256sums=(SKIP)

pkgver() {
  cd ModemManager
  git describe --tags | sed 's/-rc/rc/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd ModemManager

  # https://bugs.archlinux.org/task/74329
  chmod -x plugins/fibocom/77-mm-fibocom-port-types.rules \
           plugins/foxconn/mm-foxconn-t77w968-carrier-mapping.conf
}

build() {
  local meson_options=(
    -D dbus_policy_dir=/usr/share/dbus-1/system.d
    -D dist_version="\"$pkgver-$pkgrel\""
    -D gtk_doc=true
    -D plugin_qcom_soc=enabled
    -D polkit=permissive
    -D vapi=true
  )

  arch-meson ModemManager build "${meson_options[@]}"
  meson compile -C build
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_modemmanager-qcom() {
  depends+=(libmm-glib-qcom=$pkgver libmm-glib.so libg{lib,object,io,module}-2.0.so libsystemd.so libgudev-1.0.so
            libqmi-glib.so libmbim-glib.so)
  optdepends=('usb_modeswitch: install if your modem shows up as a storage drive')
  provides=(modemmanager=$pkgver)
  meson install -C build --destdir "$pkgdir"

  cd "$pkgdir"
  _pick libmm usr/include
  _pick libmm usr/lib/girepository-1.0
  _pick libmm usr/lib/libmm-glib.so*
  _pick libmm usr/lib/pkgconfig
  _pick libmm usr/share/gir-1.0
  _pick libmm usr/share/gtk-doc/html/libmm-glib
  _pick libmm usr/share/vala
}

package_libmm-glib-qcom() {
  pkgdesc="ModemManager library"
  depends=(libg{lib,object,io}-2.0.so)
  provides=(libmm-glib=$pkgver libmm-glib.so)

  mv libmm/* "$pkgdir"
}

# vim:set sw=2 et:
