_mode=host
_pkgname=rmtfs
pkgname="$_pkgname-git"
pkgdesc="Qualcomm Remote Filesystem Service Implementation"
pkgver=r57.b08ef6f
pkgrel=1
_arches=specific
arch=(aarch64)
license=(BSD-3-Clause)
url="https://github.com/andersson/$_pkgname"
provides=("$_pkgname")
depends=(
    qrtr-git
    udev
)
_commit=b08ef6f98ec567876d7d45f15c85c6ed00d7c463
source=(
    "git+$url#commit=$_commit"
    udev.rules
)
sha256sums=(
    SKIP
    0c2f26d40d9d18e3089c6d836b64231c4f7e0c1ca41686fe7c9d1eb495dc6bbe
)

pkgver() {
    cd "$_pkgname"

    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "$_pkgname"

    make prefix=/usr
}

package() {
    cd "$_pkgname"

    make prefix=/usr DESTDIR="$pkgdir/" install
    install -Dm644 "$srcdir"/udev.rules "$pkgdir/usr/lib/udev/rules.d/65-$_pkgname.rules"
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$_pkgname/COPYING
}
